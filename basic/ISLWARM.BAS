1 'ISLAND WAR MULTIPLAYER 1.0m
2 'ZORAN MAJCENIC, 2023
10 DEFINT A-Z:KEYOFF:COLOR 15,1,4:SCREEN 2,2:MAXFILES=3
20 DEFUSR=&H41:A=USR(0)
30 BLOAD"GRP.SC2",S
40 DEFUSR=&H44:A=USR(0)

50 ON STOP GOSUB 65500:STOP ON

110 ' FAKE SCREEN 1 MODE, SET WIDTH 30, 6 LINES SCREEN HEIGHT AND NAME TABLE BEGIN
110 ' SCREEN MODE
110 POKE &HFCAF,1
120 ' LINL32
120 POKE &HF3AF,30
130 ' LINLEN
130 POKE &HF3B0,30
140 ' CRTCNT
140 POKE &HF3B1,6
150 ' SCREEN 2 NAME TABLE BOTTOM THIRD PLUS 2 ROWS
150 Y=BASE(10)+512+2*32
160 POKE &HF922,Y MOD 256
170 POKE &HF3BD,Y MOD 256
180 POKE &HF923,Y\256
190 POKE &HF9BE,Y\256
200 LOCATE 0,0

960 'CITY SIZES (NC)
960 DIM NC(2,2)
990 'MISSILE NUMBER PER PLAYER (NM), WARHEAD NUMBER (NW), SAT NUMBER (NS), MISSILE READY (MR), PLAYER ALIVE (PA), PLAYER TYPE (PT), PLAYER ACTION (AC), SELECTED TARGET (ST), AI RELATION STATUS (RS)
990 DIM NM(2):DIM NW(2):DIM NS(2):DIM MR(2):DIM PA(2):DIM PT(2):DIM AC(2):DIM ST(2):DIM RS(2,2)
1000 'SECONDARY EVENTS (BC), ACTION RESULTS FOR PRIMARY ACTIONS (RZ) AND SECONARY EVENTS (RY)
1000 DIM BC(2):DIM RZ(2,2):DIM RY(2,2)
1010 'MAX DAMAGES, 0=MISSILE, 1=PROPAGANDA PER CITY, 2=WARHEAD EXPLOSION, 3=RIOTS
1010 DIM D(3):D(0)=50:D(1)=3:D(2)=25:D(3)=5

5020 ON INTERVAL=100 GOSUB 41000:INTERVAL ON

5500 'BANNER AND GAME TYPE CHOICE
5500 PRINT "********************":PRINT "* ISLAND WAR v1.0m *":PRINT "********************"
5510 PRINT "Select game mode:"
5520 PRINT "1 - host"
5530 PRINT "2 - client"
5540 PRINT "choice: ";
5550 A$=INKEY$:IF A$="" GOTO 5550
5560 IF A$="1" THEN GM=0:GOTO 5590
5570 IF A$="2" THEN GM=1:GOTO 5590
5580 PRINT "invalid choice ";A$:GOTO 5540
5590 ON GM+1 GOTO 5600,5810
5600 'SERVER MODE
5600 PRINT "Number of clients (0-2): ";
5610 A$=INKEY$:IF A$="" GOTO 5610
5620 PRINT A$:IF A$<>"0" AND A$<>"1" AND A$<>"2" THEN PRINT "Invalid number":GOTO 5600
5630 ON VAL(A$)+1 GOTO 5640,5650,5700
5640 'PURE LOCAL GAME WITHOUT REMOTE HUMAN PLAYERS
5640 HP=1:PT(0)=0:PT(1)=1:PT(2)=0:GOTO 5760
5650 'ONE REMOTE PLAYER
5650 HP=1:PT(0)=2:PT(1)=1:PT(2)=0
5660 'INIT COM0 AND NOTIFY FIRST REMOTE CLIENT THAT IT CONTROLS ISLAND A
5660 _COMINI("0:",1200,1200,0)
5670 OPEN "COM0:" AS #1
5680 PRINT #1,0
5690 GOTO 5760
5700 'TWO REMOTE PLAYERS
5700 HP=1:PT(0)=2:PT(1)=1:PT(2)=2
5710 'INIT COM1 AND NOTIFY SECOND REMOTE CLIENT THAT IT CONTROLS ISLAND C
5720 _COMINI("1:",1200,1200,0)
5730 OPEN "COM1:" AS #2
5740 PRINT #2,2
5750 GOTO 5660
5760 'HOSTED GAME SET UP, INIT RANDOM SEED
5760 PRINT:R=INT(RND(-TIME))
5770 '1 MISSILE, 1 WARHEAD, 1 SATELLITE, ALL PLAYERS ACTIVE, SET RANDOM INIT RELATIONSHIPS
5770 FOR I=0 TO 2
5780 NM(I)=1:NW(I)=1:NS(I)=1:PA(I)=1:MR(I)=0:FOR K=0 TO 2:RS(I,K)=INT(RND(1)*10)-5:NC(I,K)=100:NEXT K
5790 NEXT I
5800 GOTO
5810 'CLIENT MODE, INIT COM0 AND READ WHICH PLAYER IS CONTROLLED LOCALLY

5570 'DRAW CITIES
5570 GOSUB 42000

9999 'PRINT INVENTORY
9999 GOTO 20000
10000 'MAIN LOOP
10000 'CHECK HOW MANY PLAYERS ARE LEFT PLAYING
10000 IF PA(0)+PA(1)+PA(2)>1 GOTO 10020
10010 IF PA(HP)=1 THEN PRINT "CONGRATULATIONS ! YOU WIN !":GOTO 5500
10011 FOR J=0 TO 2
10012 IF PA(J)=1 AND HP<>J THEN PRINT "PLAYER ";CHR$(J+65);" WINS":PRINT "BETTER LUCK NEXT TIME":GOTO 5500
10013 NEXT J
10014 PRINT "NOBODY WINS AND":PRINT "THE WORLD IS DESTROYED !":GOTO 5500

10020 IF PA(HP)=0 GOTO 10200 ELSE PRINT "command: ";
10030 A$=INKEY$:IF A$="" GOTO 10030
10040 PRINT A$
10050 IF A$="A" OR A$="a" GOTO 15000
10060 IF A$="M" OR A$="m" GOTO 16000
10070 IF A$="D" OR A$="d" GOTO 17000
10080 IF A$="L" OR A$="l" GOTO 18000
10090 IF A$="P" OR A$="p" GOTO 19000
10100 IF A$="I" OR A$="i" GOTO 20000
10110 PRINT "unknown command ";A$
10120 GOTO 10020

10200 'PROCESS AI PLAYERS
10200 IF PT(0)=0 THEN I=0:GOSUB 20100
10210 IF PT(2)=0 THEN I=2:GOSUB 20100
10600 'PROCESS RESULTS OF SELECTED ACTIONS ON SERVER 
10600 FOR I=0 TO 2
10610 IF PA(I)=0 GOTO 10710
10620 ON AC(I) GOTO 10710,10660,10710,10680
10630 'AC=0 MANUFACTURE
10630 RZ(I,0)=INT(0.3+RND(1)+(NC(I,0)+NC(I,1)+NC(I,2))*RND(1)/100)
10640 RZ(I,1)=INT(0.3+RND(1)+(NC(I,0)+NC(I,1)+NC(I,2))*RND(1)/100)
10650 RZ(I,2)=INT(RND(1)/2+(NC(I,0)+NC(I,1)+NC(I,2))*RND(1)/200):GOTO 10710
10660 'AC=2 LAUNCH MISSILE
10660 RZ(I,0)=INT(RND(1)*D(0))
10670 GOTO 10710
10680 'AC=4 PROPAGANDA
10680 FOR J=0 TO 2
10690 RZ(I,J)=INT(RND(1)*D(1))
10700 NEXT J
10710 NEXT I 

11000 'GET SECONDARY EVENTS
11000 FOR I=0 TO 2
11010 IF PA(I)=0 THEN BC(I)=0:GOTO 11060
11020 'WARHEAD EXPLOSION = 0.8% x NUM WARHEADS
11020 IF NW(I)>1 AND RND(1)<0.008*NW(I) THEN L=D(2):GOSUB 54000:BC(I)=1:GOTO 11060
11030 'RIOTS = 0.03% x CITIZEN NUMBER
11030 T=NC(I,0)+NC(I,1)+NC(I,2)
11040 IF RND(1)<T*0.0003 THEN L=D(3):GOSUB 54000:BC(I)=2:GOTO 11060
11050 BC(I)=0
11060 NEXT I

11940 'FIRST DEPLOY DEFENSE SATELLITES
11940 IF AC(0)<>3 GOTO 11960
11950 PUT SPRITE 9,(180,0),1,10:PUT SPRITE 22,(180,0),6,11
11960 IF AC(1)<>3 GOTO 11980
11970 PUT SPRITE 10,(240,34),1,10:PUT SPRITE 21,(240,34),6,11
11980 IF AC(2)<>3 GOTO 12000
11990 PUT SPRITE 11,(0,75),1,10:PUT SPRITE 20,(0,75),6,11 
12000 'EXECUTE ACTIONS AND APPLY RESULTS
12000 FOR I=0 TO 2
12010 IF PA(I)=0 GOTO 14000
12020 ON AC(I) GOTO 12230,12300,13000,13010
12040 'MANUFACTURE
12040 'ANIMATE FACTORY
12040 IF I=0 THEN X=152:Y=3:S1=9:S2=22:GOTO 12070
12050 IF I=1 THEN X=160:Y=44:S1=10:S2=21:GOTO 12070
12060 X=90:Y=85:S1=11:S2=20
12070 C=0:K=0
12075 SOUND 0,255:SOUND 1,14:SOUND 8,9:SOUND 13,10:SOUND 6,245:SOUND 7,182
12080 PUT SPRITE S1,(X+C,Y),1,14:PUT SPRITE S2,(X+C,Y),C+14,15
12090 TIME=0
12100 IF TIME<10 GOTO 12100
12110 K=K+1:IF K>8 THEN PUT SPRITE S1,(0,209):PUT SPRITE S2,(0,209):GOSUB 43000:GOTO 12130
12120 C=(C+1) MOD 2:GOTO 12080
12130 'DISPLAY RESULTS FOR LOCAL HUMAN PLAYER ONLY
12130 IF HP<>I GOTO 12190
12140 PRINT "You manufacture: ";
12150 IF (RZ(I,0)+RZ(I,1)+RZ(I,2))=0 THEN PRINT "nothing." ELSE PRINT
12160 IF RZ(I,0)>0 THEN PRINT RZ(I,0);" missile";:IF RZ(I,0)>1 THEN PRINT "s" ELSE PRINT
12170 IF RZ(I,1)>0 THEN PRINT RZ(I,1);" warhead";:IF RZ(I,1)>1 THEN PRINT "s" ELSE PRINT
12180 IF RZ(I,2)>0 THEN PRINT RZ(I,2);" satellite";:IF RZ(I,2)>1 THEN PRINT "s" ELSE PRINT
12190 NM(I)=NM(I)+RZ(I,0):NW(I)=NW(I)+RZ(I,1):NS(I)=NS(I)+RZ(I,2):MR(I)=0
12200 FOR J=0 TO 2:IF J<>I THEN RS(J,I)=RS(J,I)-5
12210 NEXT J
12220 GOTO 14000
12230 'ARM MISSILE
12230 IF I=0 THEN X=152:Y=3:S1=9:S2=22:GOTO 12260
12240 IF I=1 THEN X=160:Y=44:S1=10:S2=21:GOTO 12260
12250 IF I=2 THEN X=90:Y=85:S1=11:S2=20
12260 PUT SPRITE S1,(X,Y),15,8:PUT SPRITE S2,(X,Y),1,9
12270 PLAY "V12T200O4CRCO5CCRRR"
12280 IF PLAY(0) THEN 12280
12290 MR(I)=1:NM(I)=NM(I)-1:GOTO 14000
12300 'LAUNCH MISSILE
12300 TP=ST(I)\10:TC=ST(I) MOD 10:GOSUB 38000
12420 'TARGET CITY COORDINATES DETERMINED
12420 'GET STARTING POSITION
12420 IF I=0 THEN X0=152:Y0=3:S1=9:S2=22:GOTO 12450
12430 IF I=1 THEN X0=160:Y0=44:S1=10:S2=21:GOTO 12450
12440 X0=90:Y0=85:S1=11:S2=20
12450 'INITIAL RISE
12450 SOUND 6,20:SOUND 7,183:SOUND 8,10
12455 FOR K=1 TO 16
12460 PUT SPRITE S1,(X0,Y0-K),15,17:PUT SPRITE S2,(X0,Y0-K),1,16
12470 NEXT K   
12480 'ARCH
12480 IF ABS(X0-X1)>ABS(Y0-16-Y1) GOTO 12485
12481 J=ABS(Y0-16-Y1):IF Y0-16<Y1 THEN T=18 ELSE T=16
12482 GOTO 12490
12485 J=ABS(X0-X1):IF X0<X1 THEN T=22 ELSE T=20
12490 DX#=CDBL(X1-X0)/J:DY#=CDBL(Y1-Y0+16)/J
12500 FOR K=0 TO J STEP 3
12505 X=X0+INT(K*DX#):Y=Y0-16+INT(K*DY#)
12510 PUT SPRITE S1,(X,Y),15,T+1:PUT SPRITE S2,(X,Y),1,T
12520 NEXT K
12530 'HIDE MISSILE
12530 PUT SPRITE S1,(0,209):PUT SPRITE S2,(0,209)
12540 MR(I)=0:NW(I)=NW(I)-1
12550 'CALCULATE DAMAGE
12550 IF AC(TP)<>3 GOTO 12580
12560 PRINT"Missile shot down!"
12565 RS(TP,I)=RS(TP,I)-1
12570 GOSUB 43000:GOTO 14000
12580 T=RZ(I,0)
12590 IF T>NC(TP,TC) THEN T=NC(TP,TC)
12595 PRINT "Missile hits ";
12600 IF T>0 THEN PRINT "killing":PRINT T;"million" ELSE PRINT "but everybody takes shelter!"
12610 NC(TP,TC)=NC(TP,TC)-T:RS(TP,I)=RS(TP,I)-T:RS(I,TP)=RS(I,TP)+T
12620 GOSUB 38200:GOSUB 42000:GOTO 14000

13000 'DEFEND
13000 NS(I)=NS(I)-1:MR(I)=0:GOTO 14000

13010 'PROPAGANDA
13010 IF I=0 THEN X=152:Y=3:S1=9:S2=22:GOTO 13040
13020 IF I=1 THEN X=160:Y=44:S1=10:S2=21:GOTO 13040
13030 X=90:Y=85:S1=11:S2=20
13040 C=0:K=0
13050 SOUND 0,0:SOUND 1,0:SOUND 8,9:SOUND 13,10:SOUND 7,190
13060 PUT SPRITE S1,(X,Y),1,12:PUT SPRITE S2,(X,Y),15,13
13070 FOR J=200 TO 255:SOUND 0,J:NEXT J
13080 FOR J=255 TO 200 STEP -1:SOUND 0,J:NEXT J
13090 K=K+1:IF K>6 THEN PUT SPRITE S1,(0,209):PUT SPRITE S2,(0,209):GOSUB 43000:GOTO 13130
13100 C=(C+1) MOD 2
13110 IF C=0 THEN PUT SPRITE S2,(X,Y),15,13 ELSE PUT SPRITE S2,(0,209)
13120 GOTO 13070
13130 S=0:FOR J=0 TO 2
13140 IF RZ(I,J)>NC(ST(I),J) THEN T=NC(ST(I),J) ELSE T=RZ(I,J)
13150 S=S+T:NC(ST(I),J)=NC(ST(I),J)-T:NEXT J
13151 K=32767:FOR J=0 TO 2:IF NC(I,J)<K AND NC(I,J)>0 THEN K=NC(I,J):L=J
13152 NEXT J:IF K=32767 THEN S=0 ELSE NC(I,L)=NC(I,L)+S
13160 IF ST(I)<>HP GOTO 13200
13170 PRINT "Propaganda hits";
13180 IF S=0 THEN PRINT " but nobody cares" ELSE PRINT ":":PRINT S;"million leave"
13190 GOTO 13230
13200 IF I<>HP GOTO 13230
13210 PRINT "Your sweet words ";
13220 IF S=0 THEN PRINT "are ignored  by everyone" ELSE PRINT "bring:":PRINT S;"million"
13230 RS(ST(I),I)=RS(ST(I),I)-S:RS(I,ST(I))=RS(I,ST(I))+S:MR(I)=0
13240 'REDRAW CITIES
13240 GOSUB 42000

14000 NEXT I
14010 'HIDE DEFENSE SATELLITES
14010 IF AC(0)<>3 GOTO 14030
14020 PUT SPRITE 9,(0,209):PUT SPRITE 22,(0,209)
14030 IF AC(1)<>3 GOTO 14050
14040 PUT SPRITE 10,(0,209):PUT SPRITE 21,(0,209)
14050 IF AC(2)<>3 GOTO 14100
14060 PUT SPRITE 11,(0,209):PUT SPRITE 20,(0,209) 

14100 'PROCESS SECONDARY EVENTS
14100 FOR I=0 TO 2
14110 IF BC(I)=0 GOTO 14980
14130 TP=I:FOR J=0 TO 2:IF RY(I,J)>0 THEN TC=J:T=RY(I,J)
14150 NEXT J
14160 IF T>NC(I,TC) THEN T=NC(I,TC)
14165 IF BC(I)=2 GOTO 14200
14170 'WARHEAD EXPLOSION
14170 IF I=HP THEN PRINT "Warhead explodes in a":PRINT "warehouse killing";T;"million" ELSE PRINT "Nuclear disaster detected"
14180 NC(I,TC)=NC(I,TC)-T:NW(I)=NW(I)-1
14190 S1=12:GOSUB 38000:GOSUB 38200:GOSUB 42000:GOTO 14980
14200 'RIOTS
14210 GOSUB 38000:PUT SPRITE 12,(X1+16,Y1-2),1,28:PUT SPRITE 13,(X1+16,Y1-2),15,29
14220 PRINT "Riots";:IF HP=I THEN PRINT "!";T;"million dead" ELSE PRINT " detected"
14230 NC(I,TC)=NC(I,TC)-T
14240 PLAY "O4T200V10F#CF#CF#C"
14250 IF PLAY(0)=-1 GOTO 14250
14260 PUT SPRITE 12,(0,209):PUT SPRITE 13,(0,209)

14980 NEXT I
14990 FOR J=0 TO 2:IF NC(J,0)+NC(J,1)+NC(J,2)=0 THEN PA(J)=0 ELSE PA(J)=1
14995 NEXT J:GOTO 10000

15000 'ARM MISSILE COMMAND
15000 PRINT "-Arm missile-"
15010 IF MR(HP)=1 THEN PRINT "Missile already armed":GOTO 10000
15020 IF NM(HP)=0 THEN PRINT "No missiles to arm":GOTO 10000
15030 GOSUB 39000
15040 IF A$="N" GOTO 10000
15050 PRINT "Arming missile"
15060 AC(HP)=1:GOTO 10200

16000 'MANUFACTURE COMMAND
16000 PRINT "-Manufacture weapons-"
16010 GOSUB 39100
16020 GOSUB 39000
16030 IF A$="N" GOTO 10000
16040 PRINT "Preparing production"
16050 MR(HP)=0:AC(HP)=0:GOTO 10200

17000 'DEFENSE SATELLITE COMMAND
17000 PRINT "-Launch defense satellite-"
17010 IF NS(HP)=0 THEN PRINT "No defense satellites":GOTO 10000
17020 GOSUB 39100
17030 GOSUB 39000
17040 IF A$="N" GOTO 10000
17050 PRINT "Preparing satellite"
17060 MR(HP)=0:AC(HP)=3:GOTO 10200

18000 'LAUNCH MISSILE COMMAND
18000 PRINT "-Launch missile-"
18010 IF MR(HP)=0 THEN PRINT "Arm missile first":GOTO 10000
18020 IF NW(HP)=0 THEN PRINT "No warhead to mount":GOTO 10000
18030 GOSUB 39500:GOSUB 39700
18040 INPUT "Enter target city";A$
18050 IF LEN(A$)<>2 GOTO 18040
18060 B$=LEFT$(A$,1):C$=RIGHT$(A$,1)
18070 IF (B$="A" OR B$="a") AND HP<>0 AND PA(0)=1 THEN TP=0:GOTO 18110
18080 IF (B$="B" OR B$="b") AND HP<>1 AND PA(1)=1 THEN TP=1:GOTO 18110
18090 IF (B$="C" OR B$="c") AND HP<>2 AND PA(2)=1 THEN TP=2:GOTO 18110
18100 GOTO 18040
18110 IF C$="1" AND NC(TP,0)>0 THEN TC=0:GOTO 18150
18120 IF C$="2" AND NC(TP,1)>0 THEN TC=1:GOTO 18150
18130 IF C$="3" AND NC(TP,2)>0 THEN TC=2:GOTO 18150
18140 GOTO 18040
18150 GOSUB 39600:GOSUB 39800
18160 GOSUB 39000
18170 IF A$="N" GOTO 10000
18180 PRINT "Preparing launch"
18190 AC(HP)=2:ST(HP)=TP*10+TC:GOTO 10200

19000 'PROPAGANDA COMMAND
19000 PRINT "-Propaganda-"
19010 GOSUB 39100
19020 GOSUB 39500
19030 PRINT "Target player? ";
19040 A$=INKEY$:IF A$="" GOTO 19040
19050 PRINT A$
19060 IF (A$="A" OR A$="a") AND HP<>0 AND PA(0)=1 THEN TP=0:GOTO 19100
19070 IF (A$="B" OR A$="b") AND HP<>1 AND PA(1)=1 THEN TP=1:GOTO 19100
19080 IF (A$="C" OR A$="c") AND HP<>2 AND PA(2)=1 THEN TP=2:GOTO 19100
19090 GOTO 19030
19100 GOSUB 39600
19110 GOSUB 39000
19120 IF A$="N" GOTO 10000
19130 PRINT "Preparing propaganda"
19140 AC(HP)=4:ST(HP)=TP:MR(HP)=0:GOTO 10200

20000 'INVENTORY COMMAND
20000 PRINT "You have:"
20010 PRINT NM(HP);"missile";:IF NM(HP)<>1 THEN PRINT "s" ELSE PRINT
20020 PRINT NW(HP);"warhead";:IF NW(HP)<>1 THEN PRINT "s" ELSE PRINT
20030 PRINT NS(HP);"satellite";:IF NS(HP)<>1 THEN PRINT "s" ELSE PRINT
20040 PRINT NC(HP,0)+NC(HP,1)+NC(HP,2);"million people"
20050 GOTO 10000

20100 'PROCESS AI MOVES, I=CURRENT AI PLAYER
20100 'IF MISSILE ARMED, FIND TARGET
20100 IF MR(I)=0 OR NW(I)=0 GOTO 20150
20110 'SELECT PLAYER WE HATE MOST
20110 K=32767:FOR J=0 TO 2:IF J<>I AND PA(J)=1 AND RS(I,J)<K THEN K=RS(I,J):L=J
20115 NEXT J
20120 'SELECT LARGEST CITY
20120 K=0:FOR J=0 TO 2:IF NC(L,J)>0 AND NC(L,J)>K THEN K=NC(L,J):C=J
20125 NEXT J
20130 'ENCODE TARGET AND SET ACTION
20130 AC(I)=2:ST(I)=L*10+C
20140 RETURN
20150 'CHECK IF SOMEONE ELSE HAS MISSILE READY TO LAUNCH AND WE CAN DEFEND
20150 IF NS(I)=0 GOTO 20190
20160 IF I<>0 AND PA(0)=1 AND MR(0)=1 AND RS(I,0)<0 THEN AC(I)=3:MR(I)=0:RETURN
20170 IF I<>1 AND PA(1)=1 AND MR(1)=1 AND RS(I,1)<0 THEN AC(I)=3:MR(I)=0:RETURN
20180 IF I<>2 AND PA(2)=1 AND MR(2)=1 AND RS(I,2)<0 THEN AC(I)=3:MR(I)=0:RETURN
20190 'CHECK IF WE ARE DOWN TO 0 STOCKS OF MISSILES OR WARHEADS, GO FOR MANUFACTURE WITH 50% CHANCE
20190 IF (NM(I)=0 OR NW(I)=0) AND RND(1)<0.5 THEN AC(I)=0:MR(I)=0:RETURN
20200 '50-50 ARM MISSILE OR PROPAGANDA
20200 IF NM(I)>0 AND NW(I)>0 AND RND(1)<0.5 THEN AC(I)=1:RETURN
20210 'PROPAGANDA
20210 'SELECT PLAYER WE HATE MOST
20210 K=32767:FOR J=0 TO 2:IF J<>I AND PA(J)=1 AND RS(I,J)<K THEN K=RS(I,J):L=J
20215 NEXT J
20220 AC(I)=4:ST(I)=L:MR(I)=0:RETURN

38000 'GET TARGET CITY COORDS, TP=TARGET PLAYER, TC=TARGET CITY
38000 IF TP<>0 GOTO 38040
38010 IF TC=0 THEN X1=20:Y1=15:RETURN
38020 IF TC=1 THEN X1=57:Y1=27:RETURN
38030 X1=90:Y1=3:RETURN
38040 IF TP<>1 GOTO 38080
38050 IF TC=0 THEN X1=97:Y1=56:RETURN
38060 IF TC=1 THEN X1=168:Y1=68:RETURN
38070 X1=200:Y1=44:RETURN
38080 IF TC=0 THEN X1=34:Y1=85:RETURN
38090 IF TC=1 THEN X1=72:Y1=109:RETURN
38100 X1=120:Y1=97:RETURN

38200 'ANIMATE EXPLOSION, S1, X1, Y1
38200 SOUND 6,255:SOUND 7,135
38210 PUT SPRITE S1,(X1,Y1-4),11,24
38220 FOR J=1500 TO 1125 STEP -5:SOUND 8,J/100:NEXT J
38230 PUT SPRITE S1,(X1,Y1-4),9,25
38240 FOR J=1125 TO 750 STEP -5:SOUND 8,J/100:NEXT J
38250 PUT SPRITE S1,(X1,Y1-4),8,26
38260 FOR J=750 TO 325 STEP -5:SOUND 8,J/100:NEXT J
38270 PUT SPRITE S1,(X1,Y1-4),6,27
38280 FOR J=325 TO 0 STEP -5:SOUND 8,J/100:NEXT J
38290 PUT SPRITE S1,(0,209):GOTO 43000

39000 'CONFIRM PROMPT (Y/N)
39000 PRINT "Confirm action (y/n):";
39010 A$=INKEY$:IF A$="" GOTO 39010
39020 PRINT A$
39030 IF A$="y" THEN A$="Y":RETURN
39040 IF A$="n" THEN A$="N":RETURN
39050 IF A$="Y" OR A$="N" THEN RETURN
39060 GOTO 39000

39100 'SCRAP MISSILE WARNING
39100 IF MR(HP)=1 THEN PRINT "This will scrap armed missile"
39110 RETURN

39200 'WRITE ISLAND LETTERS
39200 OPEN "GRP:" FOR OUTPUT AS #3
39210 IF HP<>0 AND PA(0)=1 THEN PSET (106,29),10:PRINT#3,"A"
39220 IF HP<>1 AND PA(1)=1 THEN PSET (215,67),10:PRINT#3,"B"
39230 IF HP<>2 AND PA(2)=1 THEN PSET (20,108),10:PRINT#3,"C"
39240 CLOSE #3:POKE &HFCAF,1
39250 RETURN

39300 'WRITE CITY NUMBERS
39300 OPEN "GRP:" FOR OUTPUT AS #3
39310 IF HP=0 OR PA(0)=0 GOTO 39350
39320 IF NC(0,0)>0 THEN PSET(14,22),10:PRINT#3,"1"
39330 IF NC(0,1)>0 THEN PSET(51,27),10:PRINT#3,"2"
39340 IF NC(0,2)>0 THEN PSET(84,10),10:PRINT#3,"3"
39350 IF HP=1 OR PA(1)=0 GOTO 39390
39360 IF NC(1,0)>0 THEN PSET(114,63),10:PRINT#3,"1"
39370 IF NC(1,1)>0 THEN PSET(162,68),10:PRINT#3,"2"
39380 IF NC(1,2)>0 THEN PSET(194,51),10:PRINT#3,"3"
39390 IF HP=2 OR PA(2)=0 THEN GOTO 39430
39400 IF NC(2,0)>0 THEN PSET(51,92),10:PRINT#3,"1"
39410 IF NC(2,1)>0 THEN PSET(66,109),10:PRINT#3,"2"
39420 IF NC(2,2)>0 THEN PSET(114,97),10:PRINT#3,"3"
39430 CLOSE #3:POKE &HFCAF,1:RETURN

39500 'DISPLAY ISLAND LETTERS
39500 POKE &HFCAF,2:COLOR 1:GOTO 39200

39600 'HIDE ISLAND LETTERS
39600 POKE &HFCAF,2:COLOR 10:GOTO 39200

39700 'DISPLAY CITY NUMBERS
39700 POKE &HFCAF,2:COLOR 13:GOTO 39300

39800 'HIDE CITY NUMBERS
39800 POKE &HFCAF,2:COLOR 10:GOTO 39300

41000 ' ON INTERVAL ROUTINE
41000 R=INT(RND(1)*256):VPOKE BASE(12)+1,R:VPOKE BASE(12)+2049,R
41990 RETURN

42000 ' DRAW CITIES BASED ON NUMBER OF PEOPLE
42000 IF NC(0,0)=0 THEN T=6 ELSE IF NC(0,0)<10 THEN T=4 ELSE IF NC(0,0)<50 THEN T=2 ELSE T=0
42010 PUT SPRITE 0,(20,15),1,T:PUT SPRITE 30,(20,15),15,T+1
42020 IF NC(0,1)=0 THEN T=6 ELSE IF NC(0,1)<10 THEN T=4 ELSE IF NC(0,1)<50 THEN T=2 ELSE T=0
42030 PUT SPRITE 1,(57,27),1,T:PUT SPRITE 31,(57,27),15,T+1
42040 IF NC(0,2)=0 THEN T=6 ELSE IF NC(0,2)<10 THEN T=4 ELSE IF NC(0,2)<50 THEN T=2 ELSE T=0
42050 PUT SPRITE 2,(90,3),1,T:PUT SPRITE 29,(90,3),15,T+1
42060 IF NC(1,0)=0 THEN T=6 ELSE IF NC(1,0)<10 THEN T=4 ELSE IF NC(1,0)<50 THEN T=2 ELSE T=0
42070 PUT SPRITE 4,(97,56),1,T:PUT SPRITE 27,(97,56),7,T+1
42080 IF NC(1,1)=0 THEN T=6 ELSE IF NC(1,1)<10 THEN T=4 ELSE IF NC(1,1)<50 THEN T=2 ELSE T=0
42090 PUT SPRITE 3,(168,68),1,T:PUT SPRITE 28,(168,68),7,T+1
42100 IF NC(1,2)=0 THEN T=6 ELSE IF NC(1,2)<10 THEN T=4 ELSE IF NC(1,2)<50 THEN T=2 ELSE T=0
42110 PUT SPRITE 5,(200,44),1,T:PUT SPRITE 26,(200,44),7,T+1
42120 IF NC(2,0)=0 THEN T=6 ELSE IF NC(2,0)<10 THEN T=4 ELSE IF NC(2,0)<50 THEN T=2 ELSE T=0
42130 PUT SPRITE 8,(34,85),1,T:PUT SPRITE 23,(34,85),13,T+1
42140 IF NC(2,1)=0 THEN T=6 ELSE IF NC(2,1)<10 THEN T=4 ELSE IF NC(2,1)<50 THEN T=2 ELSE T=0
42150 PUT SPRITE 6,(72,109),1,T:PUT SPRITE 25,(72,109),13,T+1
42160 IF NC(2,2)=0 THEN T=6 ELSE IF NC(2,2)<10 THEN T=4 ELSE IF NC(2,2)<50 THEN T=2 ELSE T=0
42170 PUT SPRITE 7,(120,97),1,T:PUT SPRITE 24,(120,97),13,T+1
42990 RETURN

43000 'SILENCE
43000 DEFUSR=&H90:A=USR(0):RETURN

54000 'bonus events shared code, I=current player, L=max damage
54000 'find largest city
54000 K=0:FOR J=0 TO 2:RY(I,J)=0:IF NC(I,J)>K THEN K=NC(I,J):C=J
54010 NEXT J
54020 'get random damage up to city size
54020 T=INT(RND(1)*L)+1
54030 IF T>K THEN T=K
54040 RY(I,C)=T
54050 RETURN

65500 POKE &HF3B1,24:SCREEN 0:COLOR 15,4,4:WIDTH 40